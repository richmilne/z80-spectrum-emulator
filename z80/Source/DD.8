EMULATOR SEGMENT BYTE PUBLIC

#if !i00                        ;If this file not has been assembled before, ...
dd_dummy macro
#rx1l
        org (#vx shl 8)+i00
        xor bl,bl
        jmp bx
#er
#em
ndx macro
        xchg ax,bx
        mov al,[di]
        inc di
        cbw
        add ax,es:#1
        xchg ax,bx
#em
i00     equ 04d                 ;offset of IX instructions
i11     equ 031                 ;offset of variable RIX (should not be moved!)
#else
i00     equ 081                 ;offset of IY instructions
i11     equ 02F                 ;offset of variable RIY
#endif

;Alle blokjes staan op offset 04D

        ORG (009 SHL 8)+i00
        INC DI
        AND AH,0fc
        ADD ES:w[offset i11],CX           ;ADD IX,BC
        adc ah,0
;        xor ah,dh
;        and ah,0d7
;        xor ah,dh
;        mov es:temp_f,ah
        jx 15


        ORG (019 SHL 8)+i00
        INC DI
        AND AH,0fc
        ADD ES:w[offset i11],SI           ;ADD IX,DE
        adc ah,0
;        xor ah,dh
;        and ah,0d7
;        xor ah,dh
;        mov es:temp_f,ah
        jx 15


        ORG (021 SHL 8)+i00
        INC DI
        MOV BX,[DI]
        MOV ES:w[offset i11],BX           ;LD IX,NN
        INC DI
        INC DI
        jx 14


        ORG (022 SHL 8)+i00
        INC DI
        MOV BX,[DI]                     ;LD (NN),IX
        PUSH DX
        MOV DX,ES:w[offset i11]
        MEMP BX,DX
        POP DX
        INC DI
        INC DI
        jx 20


        ORG (023 SHL 8)+i00
        INC DI
        INC ES:WORD w[offset i11]         ;INC IX
        jx 10


        ORG (024 SHL 8)+i00
        INC DI
        SAHF
        INC ES:B[i11+1]  ;INC H
        LAHF
        OVA 8
        jx 8


        ORG (025 SHL 8)+i00
        INC DI
        SAHF
        DEC ES:B[i11+1]  ;DEC H
        LAHF
        OVS 8
        jx 8


        ORG (026 SHL 8)+i00
        INC DI
        MOV BL,[DI]
        MOV ES:B[i11+1],BL       ;LD H,N
        INC DI
        jx 11


        ORG (029 SHL 8)+i00
        INC DI
        AND AH,0fc
        MOV BX,ES:w[offset i11]
        ADD BX,BX
        MOV ES:w[offset i11],BX      ;ADD IX,IX
        adc ah,0
;        xor ah,dh
;        and ah,0d7
;        xor ah,dh
;        mov es:temp_f,ah
        jx 15


        ORG (02A SHL 8)+i00
        INC DI
        MOV BX,[DI]             ;LD IX,(NN)
        MOV BX,[BX]
        MOV ES:w[offset i11],BX
        INC DI
        INC DI
        jx 20


        ORG (02B SHL 8)+i00
        INC DI
        DEC ES:WORD w[offset i11]         ;DEC IX
        jx 10


        ORG (02C SHL 8)+i00
        INC DI
        SAHF
        INC ES:B[i11]    ;INC L
        LAHF
        OVA 8
        jx 8


        ORG (02D SHL 8)+i00
        INC DI
        SAHF
        DEC ES:B[i11]    ;DEC L
        LAHF
        OVS 8
        jx 8


        ORG (02E SHL 8)+i00
        INC DI
        MOV BL,[DI]
        MOV ES:B[i11],BL ;LD L,N
        INC DI
        jx 11


        ORG (034 SHL 8)+i00
        INC DI                  ;INC (IX)
        ndx w[offset i11]
        call testbx                     ; ***** HMc *****
        jnc >l1                         ; ***** HMc *****
        SAHF
        INC B[BX]
        LAHF
        OVA KR,23
        jx 23
l1:     jmp noinc_ixiy


        ORG (035 SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;DEC (IX)
        call testbx                       ; ***** HMc *****
        jnc >l1                           ; ***** HMc *****
        SAHF
        DEC B[BX]
        LAHF
        OVS KR,23
        jx 23
l1:     jmp nodec_ixiy


        ORG (036 SHL 8)+i00
        INC DI                  ;LD (IX),N
        ndx w[offset i11]
        PUSH AX
        MOV AL,[DI]
        INC DI
        MEMP BX,AL
        POP AX
        jx 19


        ORG (039 SHL 8)+i00
        INC DI
        AND AH,0fc
        ADD ES:w[offset i11],BP           ;ADD IX,SP
        adc ah,0
;        xor ah,dh
;        and ah,0d7
;        xor ah,dh
;        mov es:temp_f,ah
        jx 15


        ORG (044 SHL 8)+i00
        INC DI
        MOV CH,ES:B[i11+1]
        jx 8                      ;LD B,IXH


        ORG (045 SHL 8)+i00
        INC DI
        MOV CH,ES:B[i11] ;LD B,IXL
        jx 8


        ORG (046 SHL 8)+i00
        INC DI                  ;LD B,(IX)
        ndx w[offset i11]
        MOV CH,[BX]
        jx 19


        ORG (04C SHL 8)+i00
        INC DI
        MOV CL,ES:B[i11+1]
        jx 8                            ;LD C,IXH


        ORG (04D SHL 8)+i00
        INC DI
        MOV CL,ES:B[i11]                ;LD C,IXL
        jx 8


        ORG (04E SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;LD C,(IX)
        MOV CL,[BX]
        jx 19


        ORG (054 SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11+1]
        XCHG DX,SI
        MOV DH,BL
        XCHG DX,SI
        jx 8                      ;LD D,IXH


        ORG (055 SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11]
        XCHG DX,SI
        MOV DH,BL
        XCHG DX,SI
        jx 8                      ;LD D,IXL


        ORG (056 SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;LD D,(IX)
        MOV BL,[BX]
        XCHG DX,SI
        MOV DH,BL
        XCHG DX,SI
        jx 19


        ORG (05C SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11+1]
        XCHG DX,SI
        MOV DL,BL               ;LD E,IXH
        XCHG DX,SI
        jx 8


        ORG (05D SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11]        ;LD E,IXL
        XCHG DX,SI
        MOV DL,BL
        XCHG DX,SI
        jx 8


        ORG (05E SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;LD E,(IX)
        MOV BL,[BX]
        XCHG DX,SI
        MOV DL,BL
        XCHG DX,SI
        jx 19


        ORG (060 SHL 8)+i00
        INC DI
        MOV ES:B[i11+1],CH
        jx 8                      ;LD IXH,B


        ORG (061 SHL 8)+i00
        INC DI
        MOV ES:B[i11+1],CL
        jx 8                      ;LD IXH,C


        ORG (062 SHL 8)+i00
        INC DI
        XCHG DX,SI
        MOV BL,DH
        XCHG DX,SI
        MOV ES:B[i11+1],BL
        jx 8                      ;LD IXH,D


        ORG (063 SHL 8)+i00
        INC DI
        XCHG DX,SI
        MOV BL,DL
        XCHG DX,SI
        MOV ES:B[i11+1],BL
        jx 8                      ;LD IXH,E


        ORG (064 SHL 8)+i00
        INC DI
        jx 8                      ;LD IXH,IXH


        ORG (065 SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11]
        MOV ES:B[i11+1],BL
        jx 8                      ;LD IXH,IXL


        ORG (066 SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;LD H,(IX)
        MOV DH,[BX]
        jx 19


        ORG (067 SHL 8)+i00
        INC DI
        MOV ES:B[i11+1],AL
        jx 8                    ;LD IXH,A


        ORG (068 SHL 8)+i00
        INC DI
        MOV ES:B[i11],CH
        jx 8                      ;LD IXL,B


        ORG (069 SHL 8)+i00
        INC DI
        MOV ES:B[i11],CL
        jx 8                      ;LD IXL,C


        ORG (06A SHL 8)+i00
        INC DI
        XCHG DX,SI
        MOV BL,DH
        XCHG DX,SI
        MOV ES:B[i11],BL
        jx 8                      ;LD IXL,D


        ORG (06B SHL 8)+i00
        INC DI
        XCHG DX,SI
        MOV BL,DL
        XCHG DX,SI
        MOV ES:B[i11],BL
        jx 8                      ;LD IXL,E


        ORG (06C SHL 8)+i00
        INC DI
        MOV BL,ES:B[i11+1]
        MOV ES:B[i11],BL
        jx 8                      ;LD IXL,IXH


        ORG (06D SHL 8)+i00
        INC DI
        jx 8                      ;LD IXL,IXL


        ORG (06E SHL 8)+i00
        INC DI
        ndx w[offset i11]                 ;LD L,(IX)
        MOV DL,[BX]
        jx 19


        ORG (06F SHL 8)+i00
        INC DI
        MOV ES:B[i11],AL
        jx 8                      ;LD IXL,A


        ORG (070 SHL 8)+i00
        INC DI                  ;LD (IX),B
        ndx w[offset i11]
        MEMP BX,CH
        jx 19


        ORG (071 SHL 8)+i00
        INC DI                  ;LD (IX),C
        ndx w[offset i11]
        MEMP BX,CL
        jx 19


        ORG (072 SHL 8)+i00
        INC DI                  ;LD (IX),D
        ndx w[offset i11]
        XCHG SI,BX
        MEMP SI,BH
        XCHG SI,BX
        jx 19


        ORG (073 SHL 8)+i00
        INC DI                  ;LD (IX),E
        ndx w[offset i11]
        XCHG SI,BX
        MEMP SI,BL
        XCHG SI,BX
        jx 19


        ORG (074 SHL 8)+i00
        INC DI                  ;LD (IX),H
        ndx w[offset i11]
        MEMP BX,DH
        jx 19


        ORG (075 SHL 8)+i00
        INC DI                  ;LD (IX),L
        ndx w[offset i11]
        MEMP BX,DL
        jx 19


        ORG (077 SHL 8)+i00
        INC DI                  ;LD (IX),A
        ndx w[offset i11]
        MEMP BX,AL
        jx 19


        ORG (07C SHL 8)+i00
        INC DI                  ;LD A,IXH
        MOV AL,ES:B[i11+1]
        jx 8


        ORG (07D SHL 8)+i00
                                ;LD A,IXL
        INC DI
        MOV AL,ES:B[i11]
        jx 8


        ORG (07E SHL 8)+i00
                                ;LD A,(IX)
        INC DI
        ndx w[offset i11]
        MOV AL,[BX]
        jx 19


        ORG (084 SHL 8)+i00
                                ;ADD A,IXH
        INC DI
        SAHF
        ADD AL,ES:B[i11+1]
        LAHF
        OVA 8
        jx 8


        ORG (085 SHL 8)+i00
                                ;ADD A,IXL
        INC DI
        SAHF
        ADD AL,ES:B[i11]
        LAHF
        OVA 8
        jx 8


        ORG (086 SHL 8)+i00
                                ;ADD A,(IX)
        INC DI
        ndx w[offset i11]
        SAHF
        ADD AL,[BX]
        LAHF
        OVA 19
        jx 19


        ORG (08C SHL 8)+i00
                                ;ADC A,IXH
        INC DI
        SAHF
        ADC AL,ES:B[i11+1]
        LAHF
        OVA 8
        jx 8


        ORG (08D SHL 8)+i00
                                ;ADC A,IXL
        INC DI
        SAHF
        ADC AL,ES:B[i11]
        LAHF
        OVA 8
        jx 8


        ORG (08E SHL 8)+i00
                                ;ADC A,(IX)
        INC DI
        ndx w[offset i11]
        SAHF
        ADC AL,[BX]
        LAHF
        OVA 19
        jx 19


        ORG (094 SHL 8)+i00
                                ;SUB A,IXH
        INC DI
        SAHF
        SUB AL,ES:B[i11+1]
        LAHF
        OVS 8
        jx 8


        ORG (095 SHL 8)+i00
                                ;SUB A,IXL
        INC DI
        SAHF
        SUB AL,ES:B[i11]
        LAHF
        OVS 8
        jx 8


        ORG (096 SHL 8)+i00
                                ;SUB A,(IX)
        INC DI
        ndx w[offset i11]
        SAHF
        SUB AL,[BX]
        LAHF
        OVS 19
        jx 19


        ORG (09C SHL 8)+i00
                                ;SBC A,IXH
        INC DI
        SAHF
        SBB AL,ES:B[i11+1]
        LAHF
        OVS 8
        jx 8


        ORG (09D SHL 8)+i00
                                ;SBC A,IXL
        INC DI
        SAHF
        SBB AL,ES:B[i11]
        LAHF
        OVS 8
        jx 8


        ORG (09E SHL 8)+i00
                                ;SBC A,(IX)
        INC DI
        ndx w[offset i11]
        SAHF
        SBB AL,[BX]
        LAHF
        OVS 19
        jx 19

        ORG (0A4 SHL 8)+i00
        INC DI                  ;AND A,IXH
        SAHF
        AND AL,ES:B[i11+1]
        LAHF
        or ah,010
        and ah,0fd
        xor ah,al
        and ah,0d7
        xor ah,al
        mov es:temp_f,ah
        jx 8


        ORG (0A5 SHL 8)+i00
        INC DI                  ;AND A,IXL
        SAHF
        AND AL,ES:B[i11]
        LAHF
        or ah,010
        and ah,0fd
        xor ah,al
        and ah,0d7
        xor ah,al
        mov es:temp_f,ah
        jx 8


        ORG (0A6 SHL 8)+i00
        INC DI                  ;AND A,(IX)
        ndx w[offset i11]
        SAHF
        AND AL,[BX]
        LAHF
        or ah,010
        and ah,0fd
        xor ah,al
        and ah,0d7
        xor ah,al
        mov es:temp_f,ah
        jx 19


        ORG (0AC SHL 8)+i00
        INC DI                  ;XOR A,IXH
        SAHF
        XOR AL,ES:B[i11+1]
        LAHF
        and ah,0ed
        jx 8


        ORG (0AD SHL 8)+i00
        INC DI
                                ;XOR A,IXL
        SAHF
        XOR AL,ES:B[i11]
        LAHF
        and ah,0ed
        jx 8


        ORG (0AE SHL 8)+i00
        INC DI
                                ;XOR A,(IX)
        ndx w[offset i11]
        SAHF
        XOR AL,[BX]
        LAHF
        and ah,0ed
        jx 19


        ORG (0B4 SHL 8)+i00
        INC DI
                                ;OR A,IXH
        SAHF
        OR AL,ES:B[i11+1]
        LAHF
        and ah,0ed
        jx 8


        ORG (0B5 SHL 8)+i00
        INC DI
                                ;OR A,IXL
        SAHF
        OR AL,ES:B[i11]
        LAHF
        and ah,0ed
        jx 8


        ORG (0B6 SHL 8)+i00
        INC DI
                                ;OR A,(IX)
        ndx w[offset i11]
        SAHF
        OR AL,[BX]
        LAHF
        and ah,0ed
        jx 19


        ORG (0BC SHL 8)+i00
        INC DI
                                ;CMP A,IXH
        SAHF
        CMP AL,ES:B[i11+1]
        LAHF
        OVS 8
        jx 8


        ORG (0BD SHL 8)+i00
        INC DI
                                ;CMP A,IXL
        SAHF
        CMP AL,ES:B[i11]
        LAHF
        OVS 8
        jx 8


        ORG (0BE SHL 8)+i00
        INC DI
                                ;CMP A,(IX)
        ndx w[offset i11]
        SAHF
        CMP AL,[BX]
        LAHF
        OVS 19
        jx 19


        ORG (0CB SHL 8)+i00
        INC DI
        INC DI
        MOV BL,[DI]
        INC DI
        XOR BH,BH
        SHL BX,1
        PUSH ES:[BX+XDCB_TABLE]
        XCHG AX,BX
        MOV AL,[DI-2]
        CBW
        ADD AX,ES:w[offset i11]
        XCHG AX,BX
        RET


        ORG (0E1 SHL 8)+i00
        INC DI
        MOV BX,DS:[BP]          ;POP IX
        MOV ES:w[offset i11],BX
        INC BP
        INC BP
        jx 14


        ORG (0E3 SHL 8)+i00
        INC DI
        call testbp                     ; ***** HMc *****
        jnc >l1                         ; ***** HMc *****
        MOV BX,ES:w[offset i11]
        XCHG DS:[BP],BX         ;EX (SP),IX
l2:     MOV ES:w[offset i11],BX
        jx 23
l1:     mov bx,ds:[bp]
        jmp l2


        ORG (0E5 SHL 8)+i00
        INC DI
        DEC BP                  ;PUSH IX
        DEC BP
        MOV BX,ES:w[offset i11]
        MEMP BP,BX
        jx 15


        ORG (0E9 SHL 8)+i00
        MOV DI,ES:w[offset i11]           ;JP (IX)
        jx 8


        ORG (0F9 SHL 8)+i00
        INC DI
        MOV BP,ES:w[offset i11]           ;LD SP,IX
        jx 8



        dd_dummy 080,081,082,083,087
        dd_dummy 090,091,092,093,097
        dd_dummy 0A0,0A1,0A2,0A3,0A7
        dd_dummy 0B0,0B1,0B2,0B3,0B7
        dd_dummy 040,041,042,043,047
        dd_dummy 050,051,052,053,057
        dd_dummy 088,089,08A,08B,08F
        dd_dummy 098,099,09A,09B,09F
        dd_dummy 0A8,0A9,0AA,0AB,0AF
        dd_dummy 0B8,0B9,0BA,0BB,0BF
        dd_dummy 048,049,04A,04B,04F
        dd_dummy 058,059,05A,05B,05F
        dd_dummy 078,079,07A,07B,07F
        dd_dummy 0,1,2,3,4,5,6,7,8
        dd_dummy 10,11,12,13,14,15,16,17
        dd_dummy 18,19,20,21,22,23,24,26
        dd_dummy 27,28,29,30,31,32,39,40
        dd_dummy 47,48,49,50,51,55,56,58
        dd_dummy 59,60,61,62,63,118,192,193
        dd_dummy 194,195,196,197,198,199,200,201
        dd_dummy 202,204,205,206,207,208,209,210
        dd_dummy 211,212,213,214,215,216,217,218
        dd_dummy 219,220,221,222,223,224,226,228
        dd_dummy 230,231,232,234,235,236,237,238
        dd_dummy 239,240,241,242,243,244,245,246
        dd_dummy 247,248,250,251,252,253,254,255

        ORG 0FFEF
        NOP



EMULATOR ENDS
