;
;Source code of TAP2TAPE.Z80, 22/3/93 Gerton Lunter
;

   10        ORG  16384
   20 AGAIN  DI
   30        LD   IX,START      ;The block is loaded at START
   40        LD   SP,IX         ;Initialise stack pointer
   50        LD   DE,#FFFF      ;Maximum length of block
   60        CP   A             ;Signal 'Do not check flag byte'
   70        SCF                ;Signal 'Load, not verify'
   80        EX   AF,AF'
   90        LD   A,15
  100        OUT  (254),A       ;White border, MIC off.
  110        CALL 1378          ;Load the block. After the call, H contains the
  120        LD   A,H           ; checksum which is zero if there has been no
  130        CP   1             ; tape error, and DE is decreased by the number of
  140        LD   HL,#FFFE      ; bytes loaded (including flag and checksum)
  150        SBC  HL,DE         ;Compute the number of bytes to be saved. If no
  160        JR   C,STOP        ; tape error, then re-compute checksum on saving.
  170        EX   DE,HL         ; If there are no bytes to be saved, quit the
  180        LD   IX,START+1    ; emulator, otherwise save the block.
  190        LD   A,(IX-1)      ;Fetch flag byte
  200        CALL #04C6         ;Save it
  210        LD   B,2
  220 WAIT   DEC  DE            ;Wait about one second
  230        LD   A,D
  240        OR   E
  250        JR   NZ,WAIT
  260        DJNZ WAIT
  270        JR   AGAIN
  280 STOP   DEFB #ED,#FF       ;This quits the emulator
  290        DEFW 0,0,0         ;Three words is enough for the stack
  300 START  EQU  $

